Program.Sub.ScreenSU.Start
Gui.Form1..Create
Gui.Form1..Caption("GCG 5666 - Quote Directory Maintenance")
Gui.Form1..Size(9615,2415)
Gui.Form1..MinX(0)
Gui.Form1..MinY(0)
Gui.Form1..Position(0,0)
Gui.Form1..BackColor(-2147483633)
Gui.Form1..MaxButton(False)
Gui.Form1..MousePointer(0)
Gui.Form1..Sizeable(False)
Gui.Form1..Event(UnLoad,Form1_UnLoad)
Gui.Form1.txtSourceDir.Create(TextBox,"",True,6645,300,0,2055,195,False,0,"Arial",8,-2147483643,1)
Gui.Form1.lbl1.Create(Label,"Source Directory",True,1710,255,0,165,270,True,0,"Arial",8,-2147483633,0)
Gui.Form1.lbl2.Create(Label,"Link Directory",True,1575,255,0,180,660,True,0,"Arial",8,-2147483633,0)
Gui.Form1.cmdSave.Create(Button)
Gui.Form1.cmdSave.Size(855,375)
Gui.Form1.cmdSave.Position(4305,1290)
Gui.Form1.cmdSave.Caption("Save")
Gui.Form1.cmdSave.Event(Click,cmdSave_Click)
Gui.Form1.txtLinkDir.Create(TextBox,"",True,6645,300,0,2055,585,False,0,"Arial",8,-2147483643,1)
Gui.Form1.cmdBrowseBaseDir.Create(Button)
Gui.Form1.cmdBrowseBaseDir.Size(555,375)
Gui.Form1.cmdBrowseBaseDir.Position(8745,135)
Gui.Form1.cmdBrowseBaseDir.Caption("^")
Gui.Form1.cmdBrowseBaseDir.Event(Click,cmdBrowseBaseDir_Click)
Gui.Form1.cmdBrowseTemplateDir.Create(Button)
Gui.Form1.cmdBrowseTemplateDir.Size(555,375)
Gui.Form1.cmdBrowseTemplateDir.Position(8745,555)
Gui.Form1.cmdBrowseTemplateDir.Caption("^")
Gui.Form1.cmdBrowseTemplateDir.Event(Click,cmdBrowseTemplateDir_Click)
Gui.Form1.lbl3.Create(Label,"Doc Control Group ID",True,1575,450,0,165,1020,True,0,"Arial",8,-2147483633,0)
Gui.Form1.txtGroupID.Create(TextBox,"-1",True,1650,300,0,2055,975,True,0,"Arial",8,-2147483643,1)
Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start
Program.Sub.Preflight.End

Program.Sub.Main.Start
'Author: Daniel Duncan
'Customer: Load King
'Program Name: Quote Directory Maintenance
'Date Started: 9/5/19
'Description: This project is placed in Estimating / Routing & Quote Management.  This program allows the user to maintain base and template directories, which are used in the other script in this project to store files related to each quote.
'Quote: 11685-0
'GAP: 5666
f.Intrinsic.Control.Try
	f.Intrinsic.Control.CallSub(GetData)
	gui.Form1..Show
f.Intrinsic.Control.Catch
	f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
f.Intrinsic.Control.EndTry
Program.Sub.Main.End

Program.Sub.Form1_UnLoad.Start
	f.Intrinsic.Control.End
Program.Sub.Form1_UnLoad.End

Program.Sub.cmdSave_Click.Start
	f.Intrinsic.Control.Try
		v.Local.sSQL.Declare(string)
		f.Intrinsic.String.Build("insert into GCG_5666_QTELINKDIR values ('{0}','{1}','{2}');",v.Screen.Form1!txtSourceDir.text,v.Screen.Form1!txtLinkDir.text,v.Screen.Form1!txtGroupID.text,v.Local.sSQL)
		f.ODBC.Connection!con.opencompanyconnection
			f.ODBC.Connection!con.execute("delete from GCG_5666_QTELINKDIR;")
			f.ODBC.Connection!con.execute(v.Local.sSQL)
		f.ODBC.Connection!con.close
		f.Intrinsic.UI.Msgbox("Values saved successfully")
	f.Intrinsic.Control.Catch
		f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
	f.Intrinsic.Control.EndTry
Program.Sub.cmdSave_Click.End

Program.Sub.GetData.Start
	f.Intrinsic.Control.Try
		f.ODBC.Connection!con.opencompanyconnection
		f.Data.DataTable.CreateFromSQL("dtData","con","select * from GCG_5666_QTELINKDIR")
		f.ODBC.Connection!con.Close
		f.Intrinsic.Control.If(v.DataTable.dtData.RowCount,>,0)
			gui.Form1.txtSourceDir.Text(v.DataTable.dtData(0).Qte_Source_Dir!FieldValTrim)
			gui.Form1.txtLinkDir.Text(v.DataTable.dtData(0).Qte_Link_Dir!FieldValTrim)
			gui.Form1.txtGroupID.Text(v.DataTable.dtData(0).GroupID!FieldValTrim)
		f.Intrinsic.Control.EndIf
	f.Intrinsic.Control.Catch
		f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
	f.Intrinsic.Control.EndTry
Program.Sub.GetData.End

Program.Sub.ErrorMsg.Start
v.Local.sError.Declare

'Closes ODBC connection, default to connection : "con"
f.Intrinsic.Control.If(v.ODBC!con.State, =, 1)
	f.ODBC.Connection!con.Close
f.Intrinsic.Control.EndIf

'Generic Error message.
f.Intrinsic.String.Build("Project GCG_5666_QuoteDirectoryMaintenance.g2u {0}{0}Subroutine: {1}{0}Error: {2} with Description: {3}", v.Ambient.NewLine, v.Args.CurrentSub, v.Ambient.ErrorNumber, v.Ambient.ErrorDescription, v.Local.sError)
f.Intrinsic.UI.Msgbox(v.Local.sError)
Program.Sub.ErrorMsg.End

Program.Sub.cmdBrowseBaseDir_Click.Start
	f.Intrinsic.Control.Try
		v.Local.sSelectedFileName.Declare(string)
		f.Intrinsic.UI.FolderBrowser("Browse for folder",v.Local.sSelectedFileName)
		f.Intrinsic.Control.If(v.Local.sSelectedFileName,!=,"***CANCEL***")
			gui.Form1.txtSourceDir.Text(v.Local.sSelectedFileName)
		F.Intrinsic.Control.EndIf
	f.Intrinsic.Control.Catch
		f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
	f.Intrinsic.Control.EndTry
Program.Sub.cmdBrowseBaseDir_Click.End

Program.Sub.cmdBrowseTemplateDir_Click.Start
	f.Intrinsic.Control.Try
		v.Local.sSelectedFileName.Declare(string)
		f.Intrinsic.UI.FolderBrowser("Browse for folder",v.Local.sSelectedFileName)
		f.Intrinsic.Control.If(v.Local.sSelectedFileName,!=,"***CANCEL***")
			gui.Form1.txtLinkDir.Text(v.Local.sSelectedFileName)
		F.Intrinsic.Control.EndIf
	f.Intrinsic.Control.Catch
		f.Intrinsic.Control.CallSub(ErrorMsg, CurrentSub, v.Ambient.CurrentSubroutine)
	f.Intrinsic.Control.EndTry
Program.Sub.cmdBrowseTemplateDir_Click.End